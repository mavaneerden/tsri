cmake_minimum_required(VERSION 4.0.3)

project("tsri"
    VERSION 0.1.0
    DESCRIPTION "Type Safe(r) Hardware Register Interface: API to more safely interface with hardware registers."
    HOMEPAGE_URL "https://github.com/mavaneerden/tsri"
    LANGUAGES CXX
)

### OPTIONS ###
set(TSRI_OUTPUT_DIRECTORY "${CMAKE_SOURCE_DIR}/registers" CACHE STRING "TSRI Generator output directory.")
set(TSRI_SVD_FILE "" CACHE STRING "SVD file used for the TSRI Generator.")
set(TSRI_NAMESPACE "" CACHE STRING "C++ namespace that encapsulates each peripheral definition. Default: no namespace.")
set(TSRI_PRETTY_CODE OFF CACHE STRING "Enable pretty code generation. This makes the generated files ~26% larger. Default: OFF")

if(TSRI_SVD_FILE STREQUAL "")
    message(FATAL_ERROR "TSRI requires an SVD file, but none was provided. Set 'TSRI_SVD_FILE' to the SVD file path.")
endif()
get_filename_component(TSRI_SVD_FILE ${TSRI_SVD_FILE}
                       REALPATH BASE_DIR "${CMAKE_SOURCE_DIR}")

set(CODE_GENERATOR_ARGUMENTS "")
if(TSRI_PRETTY_CODE STREQUAL ON)
    set(CODE_GENERATOR_ARGUMENTS "--pretty")
endif()

### CONSTANTS ###
set(TSRI_INCLUDE_DIRECTORY "include")
set(TSRI_HEADER_DIRECTORY "${TSRI_INCLUDE_DIRECTORY}/tsri")
set(TSRI_GENERATOR_DIRECTORY "${CMAKE_CURRENT_LIST_DIR}/codegen")
set(TSRI_GENERATOR "generate.py")

### CODE GENERATION ###
# Find python.
find_program(PYTHON_PROGRAM NAMES python python3)

# Find all headers that are to be generated.
execute_process(
    COMMAND ${PYTHON_PROGRAM} ${TSRI_GENERATOR} ${TSRI_SVD_FILE} ${TSRI_OUTPUT_DIRECTORY} -l
    WORKING_DIRECTORY ${TSRI_GENERATOR_DIRECTORY}
    OUTPUT_VARIABLE GENERATED_HEADERS
)

# Add commands for actual generation.
# Each header file is generated by a different command to allow parallel generation.
# This speeds up the generation significantly.
foreach(GENERATED_HEADER ${GENERATED_HEADERS})
get_filename_component(PERIPHERAL_NAME ${GENERATED_HEADER} NAME_WE)
add_custom_command(
    OUTPUT ${GENERATED_HEADER}
    COMMAND ${PYTHON_PROGRAM} ${TSRI_GENERATOR} ${TSRI_SVD_FILE} ${TSRI_OUTPUT_DIRECTORY} ${CODE_GENERATOR_ARGUMENTS} -g ${PERIPHERAL_NAME} -n --namespace "${TSRI_NAMESPACE}"
    WORKING_DIRECTORY ${TSRI_GENERATOR_DIRECTORY}
    COMMENT "Generating TSRI header file for peripheral '${PERIPHERAL_NAME}'..."
    VERBATIM
)
endforeach()

### ADD LIBRARY ###
include(GNUInstallDirs)

add_library(${PROJECT_NAME} INTERFACE
    ${TSRI_HEADER_DIRECTORY}/fields/bit_position_container.hpp
    ${TSRI_HEADER_DIRECTORY}/fields/field_types.hpp
    ${TSRI_HEADER_DIRECTORY}/fields/field.hpp
    ${TSRI_HEADER_DIRECTORY}/fields/value_container.hpp
    ${TSRI_HEADER_DIRECTORY}/registers/register_base.hpp
    ${TSRI_HEADER_DIRECTORY}/registers/register_read_only.hpp
    ${TSRI_HEADER_DIRECTORY}/registers/register_read_write.hpp
    ${TSRI_HEADER_DIRECTORY}/registers/register_write_base.hpp
    ${TSRI_HEADER_DIRECTORY}/registers/register_write_only.hpp
    ${TSRI_HEADER_DIRECTORY}/utility/concepts.hpp
    ${TSRI_HEADER_DIRECTORY}/utility/inline_macro.hpp
    ${TSRI_HEADER_DIRECTORY}/utility/type_map.hpp
    ${TSRI_HEADER_DIRECTORY}/utility/types.hpp
    ${GENERATED_HEADERS}
)

# Add include directories.
# When library is built, use the include directory. When it is installed, use install directory.
target_include_directories(
  ${PROJECT_NAME}
  INTERFACE $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/${TSRI_INCLUDE_DIRECTORY}>
            $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>)

# Set C++ standard to C++26.
target_compile_features(${PROJECT_NAME} INTERFACE cxx_std_26)

# Generate precompiled header to keep compilation times down.
foreach(GENERATED_HEADER ${GENERATED_HEADERS})
target_precompile_headers(${PROJECT_NAME} INTERFACE $<$<COMPILE_LANGUAGE:CXX>:${GENERATED_HEADER}>)
endforeach()
